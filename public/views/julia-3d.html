<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<title>My first three.js app</title>
		<script src="/three.min.js"></script>
        <script src="https://threejs.org/examples/js/controls/OrbitControls.js"></script>
        <script src="https://threejs.org/examples/js/controls/DragControls.js"></script>
        <script type="text/javascript" src="dat.gui.min.js"></script>
        <script type="text/javascript" src="threex.domevents.js"></script>
  </head>
	<style>
		body { margin: 0; }
		canvas { display: block; }
	</style>
	</head>
	<body> 
		  <script id="vertexShader" type="x-shader/x-fragment">
            varying vec2 vUv;

            void main()
            {
              vUv = uv;
              vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
              gl_Position = projectionMatrix * mvPosition;
            }
		 </script>
		  <script id="fragmentShader" type="x-shader/x-fragment">
            uniform float time;
            uniform float cR;
            uniform float cI;
            uniform float cJ;
            uniform float cK;
            uniform float camX;
            uniform float camY;
            uniform float camZ;
            uniform float zoom;
            uniform vec3 colorA;
            uniform vec3 colorB;
            uniform float t;
            varying vec2 vUv;
            uniform float power;
            uniform float rotate;

            #define PHI (sqrt(5)*0.5 + 0.5)

            //Color functions
            vec3 palette( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d )
            {
                return a + b*cos( 9.28*(c*t+d) );
            }

            void pR(inout vec2 p, float a) {
                p = cos(a)*p + sin(a)*vec2(p.y, -p.x);
            }

            //--------------------------------------------------------------------------------
            // quaternion manipulation
            //--------------------------------------------------------------------------------
            
            vec4 qSquare( vec4 a )
            {
                return vec4( a.x*a.x - dot(a.yzw,a.yzw), 2.0*a.x*(a.yzw) );
            }
            
            vec4 qCube( vec4 a )
            {
                return a * ( 4.0*a.x*a.x - dot(a,a)*vec4(3.0,1.0,1.0,1.0) );
            }
            
            //--------------------------------------------------------------------------------
            
            float lengthSquared( vec4 z ) { return dot(z,z); }
            
            // animation
            
            float DE( vec3 p )
            {   vec4 c = vec4(cR,cI,cJ,cK);
                vec4 z = vec4( p, 0.2 );
                
                float m2 = 0.0;
                vec2  t = vec2( 1e10 );
            
                float dz2 = 1.0;
                for( int i=0; i<10; i++ ) 
                {
                    // |dz|² = |3z²|²
                    dz2 *= 9.0*lengthSquared(qSquare(z));
                    
                    // z = z^3 + c		
                    z = qCube( z ) + c;
                    
                    // stop under divergence		
                    m2 = dot(z, z);		
                    if( m2>1000.0 ) break;				 
            
                    // orbit trapping ( |z|² and z_x  )
                    t = min( t, vec2( m2, abs(z.x)) );
            
                }
            
                // distance estimator: d(z) = 0.5·log|z|·|z|/|dz|   (see http://iquilezles.org/www/articles/distancefractals/distancefractals.htm)
                float d = 0.25 * log(m2) * sqrt(m2/dz2 );
            
                //return vec3( d, t );
                return d;
            }


            float scene(vec3 ray){
            
                ray = ray-vec3(0.03,0.5,1.);
                //Spin it - change the plane
                pR(ray.zx,rotate); // rotating
                
                 return max(DE(ray),0.);
            }

            vec3 estimateNormal(vec3 p) {
                float smallNumber = 0.1;
                vec3 n = vec3(
                scene(vec3(p.x + smallNumber, p.yz)) -
                scene(vec3(p.x - smallNumber, p.yz)),
                scene(vec3(p.x, p.y + smallNumber, p.z)) -
                scene(vec3(p.x, p.y - smallNumber, p.z)),
                scene(vec3(p.xy, p.z + smallNumber)) -
                scene(vec3(p.xy, p.z - smallNumber))
            );

            return normalize(n);
            }


            float lighting(vec3 origin, vec3 dir, vec3 normal) {
                vec3 lightPos = vec3(cos(time/20.)*2., sin(time), 1.);
                vec3 light = normalize(lightPos - origin);
                
                float diffuse = max(0., dot(light, normal));
                vec3 reflectedRay = 1.5 * dot(light, normal) * normal - light;
                
                float specular = max(0.3, (pow(dot(reflectedRay, light), 4.)));
                float ambient = 0.08;
                
                return ambient + diffuse + specular;

            }

            vec4 trace(vec3 rayOrigin, vec3 dir){
                vec3 ray = rayOrigin;
                float dist = 0.; 
                float totalDist = 0.;
                float maxDist = 12.;
                
                for (int i = 0; i < 80 ; i++){
                    dist = scene(ray);
                
                    if(dist < 0.001){
                        vec4 distCol = vec4(1. - vec4(totalDist/maxDist));
    
                        vec3 hsvCol = palette(
                            distCol.r,
                            colorA/255.,
                            colorB/255.,
                            vec3(.5, 1.0, .8),
                            vec3(.6, .4, .2));
                        vec4 lightingCol = vec4(lighting(rayOrigin,dir,estimateNormal(ray)));
                        return vec4(hsvCol,1.)*t + lightingCol*(1. - t);
                        
                    } 
                    totalDist += dist;
                    ray += dist * dir;
                }  
                return vec4(-1.);
            }

            vec3 lookAt(vec2 uv, vec3 camOrigin, vec3 camTarget){
                vec3 zAxis = normalize(camTarget - camOrigin);
                vec3 up = vec3(-0.03,0.4,0.2);
                vec3 xAxis = normalize(cross(up, zAxis));
                vec3 yAxis = normalize(cross(zAxis, xAxis));

                float fov = 2.;

                vec3 dir = (normalize(uv.x * xAxis + uv.y * yAxis + zAxis * fov));

                return dir;
            }


            void main() {
                vec2 v = vUv;
                v = v *4. - 2.;
                v *= pow( 0.7, 5.0 + 13.*(1.+zoom));
            
                vec3 camOrigin = vec3(camX, camY, camZ); //z axis foward and backwards
                vec3 camTarget = vec3(0, 0, 0);
                vec3 rayOrigin = vec3(camOrigin.xy, camOrigin.z - 2.5);  
            
                vec3 direction = lookAt(v, camOrigin, camTarget);
                gl_FragColor = trace(rayOrigin, direction);
            
            }
		</script>
		<script src="/3d.js"></script>
	</body>
</html>